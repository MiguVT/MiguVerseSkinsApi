plugins {
    id("com.github.johnrengelman.shadow") version "7.1.0"
    java
    alias(libs.plugins.runvelocity)
    alias(libs.plugins.idea.ext)
    alias(libs.plugins.blossom)
}

repositories {
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://repo.codemc.org/repository/maven-public/") {
        name = "codemc"
    }

}

dependencies {
    implementation("com.sparkjava:spark-core:2.9.4")
    compileOnly(libs.velocity)
    compileOnly("net.skinsrestorer:skinsrestorer-api:15.0.7")
    annotationProcessor(libs.velocity)
}

tasks {
    shadowJar {
        archiveClassifier.set("")
        mergeServiceFiles()
    }
    compileJava {
        options.encoding = Charsets.UTF_8.name()
        options.release.set(17)
    }
    clean {
        // Deletes the directory that is generated by the runVelocity plugin
        delete("run")
    }
    runVelocity {
        velocityVersion(libs.versions.velocity.get())
    }
    build {
        dependsOn(shadowJar)
    }
}

sourceSets {
    main {
        blossom {
            javaSources {
                property("version", project.version.toString())
            }
        }
    }
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))
